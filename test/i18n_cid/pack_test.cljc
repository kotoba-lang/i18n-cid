(ns i18n-cid.pack-test
  (:require [clojure.test :refer [deftest is testing]]
            [i18n-cid.pack :as p]))

(def en-catalog
  {:app/title "Welcome"
   :nav/home "Home"})

(def ja-catalog
  {:app/title "ようこそ"
   :nav/home "ホーム"})

(def id-catalog
  {:app/title "Wilkommen"
   :nav/home "Heim"})

(deftest pack-round-trip
  (let [res (p/pack :myapp {:en en-catalog :ja ja-catalog :id id-catalog})]
    (testing "car bytes present and non-empty"
      (is (some? (:car-bytes res)))
      (is (pos? (count (:car-bytes res)))))
    (testing "every locale is in the pack by its own CID"
      (is (= 3 (count (:locale-cids res))))
      (is (every? #(re-find #"bafy" %) (vals (:locale-cids res)))))
    (testing "index cid is the pack's root"
      (is (string? (:index-cid res)))
      (is (re-find #"bafy" (:index-cid res))))
    (testing "unpack restores the exact catalogs"
      (let [back (p/unpack (:car-bytes res))]
        (is (= :myapp (:project back)))
        (is (= (:index-cid res) (:index-cid back)))
        (is (= (:locale-cids res) (:locales back)))
        (is (= {:en en-catalog :ja ja-catalog :id id-catalog}
               (:catalogs back)))))))

(deftest pack-entry-index
  (let [{:keys [entries index-cid locale-cids]} (p/pack :myapp {:en en-catalog :ja ja-catalog})]
    (testing "one frame per block (2 locales + 1 index)"
      (is (= 3 (count entries))))
    (testing "index cid names a frame in the archive"
      (is (some #(= index-cid (:cid %)) entries)))
    (testing "each locale cid names a frame in the archive"
      (is (every? (fn [cid] (some #(= cid (:cid %)) entries)) (vals locale-cids))))))